buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:$forge_gradle_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'

version = "${mc_version}-${mod_version}"
group = mod_namespace
archivesBaseName = mod_id

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "run"

    mappings = mcp_mappings

    replace "@MOD_VERSION@": mod_version, "@MOD_ID": mod_id
}

runClient {
    args '--username', run_as
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    exclude "**/*.pdn"
    exclude "**/*.cubik"

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'modid': mod_id, 'version': project.version, 'mcversion': project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

repositories {
    jcenter()
    maven {
        url "http://maven.shadowfacts.net/"
    }
}

dependencies {
    deobfCompile group: 'net.shadowfacts', name: 'ShadowMC', version: shadowmc_version
    deobfCompile group: 'net.shadowfacts', name: 'Forgelin', version: forgelin_version
}
