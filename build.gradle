plugins {
    id 'fabric-loom' version '0.2.1-SNAPSHOT'
    id 'maven-publish'
    id "com.palantir.git-version" version "0.11.0"
    id "org.jetbrains.kotlin.jvm" version "1.3.21"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = "${gitVersion()}+mc${project.minecraft_normalized_version}"
group = project.maven_group

compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

minecraft {
}

repositories {
    maven { url = "http://maven.fabricmc.net" }
    maven { url = "https://kotlin.bintray.com/kotlinx" }
    maven { url = "https://minecraft.curseforge.com/api/maven" }
    maven { url = "https://maven.fabricmc.net/io/github/prospector/modmenu/ModMenu/" }
    maven { url = 'http://maven.sargunv.s3-website-us-west-2.amazonaws.com/' }
}

configurations {
    ktlint
}


dependencies {
    ktlint "com.github.shyiko:ktlint:${project.ktlint_version}"

    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}"
    modCompile "net.fabricmc:fabric-loader:${project.loader_version}"

    modCompile "net.fabricmc:fabric:${project.fabric_version}"
    include "net.fabricmc:fabric:${project.fabric_version}"

    modCompile "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"
    compileOnly "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"
    include "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"

    modCompile "cloth-config:ClothConfig:${project.cloth_config_version}"
    include "cloth-config:ClothConfig:${project.cloth_config_version}"

    modCompile "me.sargunvohra.mcmods:auto-config:${project.auto_config_version}"
    include "me.sargunvohra.mcmods:auto-config:${project.auto_config_version}"

    modCompile "io.github.prospector.modmenu:ModMenu:${project.mod_menu_version}"

    // temporary hack until MC-148461 is fixed
    compile "org.lwjgl:lwjgl-jemalloc:3.2.1"
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        filter { line -> line.replace("%VERSION%", "${project.version}") }
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

task ktlint(type: JavaExec, group: "verification") {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

task ktlintApplyToIdea(type: JavaExec, group: "formatting") {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "--apply-to-idea-project", "-y"
}
